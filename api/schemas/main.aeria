collection File extends aeria.file {}
collection TempFile extends aeria.tempFile {}
collection User extends aeria.user {
  properties {
    business str
    roles []enum @values([
      "root",
      "logistic",
      "support",
      "commercial",
      "producer",
      "callcenter"
    ])
  }
}

collection Ticket {}

collection Comment {
  icon "chat-text"
  owned "on-write"
  required {
    description
  }
  properties {
    ticket Ticket
    description str @element("textarea")
    images []File @accept(["image/*"])
  }
  functions {
    get @expose
    getAll @expose
    insert @expose
    remove @expose
    upload @expose
  }
  presets {
    crud
  }
}

collection Topic {
  icon "text-align-left"
  owned "on-write"
  table {
   title
   images
  }
  required {
    title
  }
  properties {
    title str
    description str @element("textarea")
    images []File @accept(["image/*"])
  }
  functions {
    get @expose
    getAll @expose
    insert @expose
    remove @expose
    upload @expose
  }
  presets {
    crud
  }
}

collection Ticket {
  icon "ticket"
  owned "on-write"
  filters{
    title
    topic
    status
    priority
  }
  indexes {
    title
  }
  required {
    title
    topic
    priority
    status
    description
    attached
  }
  table {
    title
    topic
    status
    priority
    created_at
  }
  properties {
    title str
    topic Topic @populate([images])
    status enum @values([
      "Open",
      "Repairing",
      "Completed"
    ] ) 
    priority enum @values([
      "Low",
      "Moderate",
      "Urgent"
    ])
    description str @element("textarea")
    attached File @accept(["image/*"])
    created_at str @format("date-time")
    updated_at str @format("date-time")
    owner User @populate([roles])
  }
  functions {
    count @expose
    get @expose
    getAll @expose
    insert @expose
    upload @expose
    remove @expose
  }
  presets {
    crud
  }
}
